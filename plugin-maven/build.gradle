ext.maven_name='EquoIDE for Maven'
ext.maven_artifact = 'equo-ide-maven-plugin'
ext.maven_desc=干.proj('gradlePlugin_equoIde_desc', 'Marketing description of EquoIDE')

String VER_MAVEN_API='3.0'

apply from: 干.file('changelog.gradle')

apply plugin: 'java'
apply plugin: 'com.adarshr.test-logger'
sourceCompatibility = java_compat
targetCompatibility = java_compat
tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}
dependencies {
	compileOnly "org.apache.maven:maven-plugin-api:$VER_MAVEN_API"
	compileOnly "org.apache.maven:maven-core:$VER_MAVEN_API"
	compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:$VER_MAVEN_API"
	compileOnly "org.eclipse.aether:aether-api:1.1.0"
	implementation project(':solstice')
	implementation 'com.diffplug.durian:durian-swt.os:4.1.1'
	testImplementation "org.apache.maven:maven-plugin-api:$VER_MAVEN_API"
	testImplementation "org.junit.jupiter:junit-jupiter:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
}
apply from: 干.file('maven.gradle')
apply from: 干.file('sonatype.gradle')
apply plugin: 'de.benediktritter.maven-plugin-development'
mavenPlugin {
	name = 'equo-ide-maven-plugin'
	artifactId = 'equo-ide-maven-plugin'
	description = 干.proj('gradlePlugin_equoIde_desc', 'Marketing description of EquoIDE')
}
jar.manifest.attributes.put('Implementation-Version', spotlessChangelog.versionNext)
test {
	useJUnitPlatform()
	dependsOn 'publishToMavenLocal'
	dependsOn ':solstice:publishToMavenLocal'
}

tasks.named('generateMavenPluginHelpMojoSources') {
	notCompatibleWithConfigurationCache('https://github.com/britter/maven-plugin-development/issues/8')
}