ext.maven_name='Solstice'
ext.maven_artifact = 'solstice'
ext.maven_desc='Single-classloader implementation of the Eclipse plugin registry and OSGi (including Declarative Services)'

apply from: 干.file('changelog.gradle')
apply plugin: 'java-library'
sourceCompatibility = java_compat
targetCompatibility = java_compat
apply from: 干.file('maven.gradle')
apply from: 干.file('sonatype.gradle')

String VER_SLF4J = '2.0.5'

dependencies {
	implementation 'com.diffplug.durian:durian-swt.os:4.1.1'
	implementation 'org.apache.felix:org.apache.felix.scr:2.2.4'

	compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
	compileOnly 'com.google.code.findbugs:annotations:3.0.1'
	api "org.slf4j:slf4j-api:$VER_SLF4J"

	testImplementation "org.slf4j:slf4j-simple:$VER_SLF4J"
	testImplementation "org.junit.jupiter:junit-jupiter:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
}

sourceSets {
	testSetup {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}
configurations {
	testImplementation.extendsFrom(testSetupImplementation)
}

file("nestedJarsNeededForTest").readLines().forEach {
	dependencies.testImplementation files(it)
}

apply plugin: 'com.diffplug.eclipse.mavencentral'
eclipseMavenCentral {
	release '4.25.0', {
		compileOnly 'org.eclipse.osgi'
		compileOnly 'org.eclipse.core.runtime'

		dep 'testSetupImplementation', 'org.eclipse.jdt.ui'
		dep 'testSetupImplementation', 'org.eclipse.jdt.debug'
		dep 'testSetupImplementation', 'org.eclipse.jdt.debug.ui'
		dep 'testSetupImplementation', 'org.eclipse.ui.ide.application'
		dep 'testSetupImplementation', 'org.osgi.service.component'
		dep 'testSetupImplementation', 'org.osgi.service.cm'
		dep 'testSetupImplementation', 'org.eclipse.equinox.cm'
		dep 'testSetupImplementation', 'org.eclipse.equinox.event'
		useNativesForRunningPlatform()
		constrainTransitivesToThisRelease()
	}
}

test {
	useJUnitPlatform()
}