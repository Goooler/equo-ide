plugins {
	id 'com.diffplug.blowdryer'
}

// dogfood
apply plugin: 'dev.equo.ide'
equoIde {
	branding().title('Equo IDE')
	welcome().openUrl('https://github.com/equodev/equo-ide/blob/main/CONTRIBUTING.md#equoide-and-solstice-developers-guide')
}

allprojects {
	apply from: 干.file('spotless.gradle')
}
spotless {
	groovyGradle {
		target '*.gradle', 'gradle/blowdryer-scripts/**/*.gradle'
	}
}
group = 干.proj('maven_group', 'the maven group')
apply from: 干.file('sonatype.gradle')

// see the top of P2_MULTITOOL.md for a checklist
tasks.register('p2multitool', Tar) {
	archiveBaseName = 'p2multitool'
	archiveVersion = project(':plugin-gradle').spotlessChangelog.versionLast
	from 'plugin-gradle/example-gradle', {
		include 'gradle/**'
		include 'gradlew'
		include 'gradlew.bat'
		include '*.gradle'
		include 'imgs/**'
	}
	from 'plugin-maven/example-maven', {
		include 'mvnw'
		include 'mvnw.cmd'
		include '.mvn/**'
		include '*.xml'
	}
	from '.', {
		include 'P2_MULTITOOL.md'
		include 'p2*.png'
	}
	destinationDirectory = file(".")
	compression = Compression.NONE
}